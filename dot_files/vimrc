"""" FROM https://github.com/VundleVim/Vundle.vim
set nocompatible              " be iMproved, required
filetype off                  " required

call plug#begin('~/.vim/plugged')
Plug 'VundleVim/Vundle.vim'
Plug 'scrooloose/nerdtree'
Plug 'altercation/vim-colors-solarized'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
Plug 'ctrlpvim/ctrlp.vim'
Plug 'powerline/fonts'
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'
Plug 'easymotion/vim-easymotion'
Plug 'mhinz/vim-signify'
Plug 'tpope/vim-fugitive'
Plug 'scrooloose/nerdcommenter'
Plug 'godlygeek/tabular'
Plug 'shougo/neocomplete.vim'
Plug 'tpope/vim-markdown'
Plug 'chazy/cscope_maps'
"Look into these:
"Plug 'xolox/vim-session'
"Plug 'terryma/vim-multiple-cursors'
"Plug 'jistr/vim-nerdtree-tabs'
"Plug 'tpope/vim-abolish'

" Allow the user to specify any custom plugins here
try
   source ~/.myvimrc_plugins
catch
   " No such file? No problem; just ignore it
endtry

" All of your Plugins must be added before the following line
call plug#end()            " required
"
" Put your non-Plugin stuff after this line
""""""""""""""""""""""""

" https://github.com/kien/ctrlp.vim/blob/master/doc/ctrlp.txt
let g:ctrlp_max_files = 0
let g:ctrlp_max_depth = 40
set wildignore+=.git\*,.hg\*,.svn\*,*.d,*.dbo,*.o,*.ti,*.a,*.so

scriptencoding utf-8
set encoding=utf-8
let mapleader = ','

"Pretty tabbing thru files
let g:airline#extensions#tabline#enabled = 1

"Easy opening of directories
" From:
" http://stackoverflow.com/questions/1708623/opening-files-in-the-same-folder-as-the-current-file-in-vim
map ,e :e <C-R>=expand("%:p:h") . "/" <CR>
map ,t :tabe <C-R>=expand("%:p:h") . "/" <CR>
map ,s :split <C-R>=expand("%:p:h") . "/" <CR>

"let g:NERDTreeDirArrows=0

"Fix C++ indentation
" g0 -- public and private now are not indented;
" +0 -- *mostly* fixes the unwanted template indentation, it still
"       gets messed up when inside a namespace and if the template
"       args span more than 1 line ... grrrr.  It looks like this is
"       a known issue:
"       https://github.com/vim-scripts/google.vim/blob/master/indent/google.vim
"
" N-s -- don't intent inside namespaces
" the
"    j1,(0,ws,Ws
" seems to fix lambda indentation:
"    https://www.systutorials.com/241820/how-to-make-vim-indent-c11-lambdas-correctly/
set cino=g0,+0,N-s,j1,(0,ws,Ws

"Don't let the surround plugin re-format code
" This is annoying when trying to block comment stuff
let g:surround_indent=0

"Dont create ~ files
set nobackup

"Dont create swap files
set noswapfile

"from http://vim.wikia.com/wiki/Show_current_function_name_in_C_programs
fun! ShowFuncName()
  let lnum = line(".")
  let col = col(".")
  echohl TermCursor
  echo getline(search("^[^ \t#/]\\{2}.*[^:]\s*$", 'bW'))
  echohl None
  call search("\\%" . lnum . "l" . "\\%" . col . "c")
endfun

map <F2> :call ShowFuncName() <CR>

set pastetoggle=<F5>

noremap \ ,

autocmd FileType * setlocal expandtab shiftwidth=3 softtabstop=3
autocmd FileType python setlocal expandtab shiftwidth=4 softtabstop=4
autocmd FileType make setlocal noexpandtab
"See
"  https://stackoverflow.com/questions/2360249/vim-automatically-removes-indentation-on-python-comments
autocmd FileType python setlocal nosmartindent

if &filetype != "python"
   " Allow Tab toggling between linux and typical c++ Standards
   function! TabToggle()
     if &expandtab
       set shiftwidth=3
       set softtabstop=0
       set noexpandtab
     else
       set shiftwidth=3
       set softtabstop=3
       set expandtab
     endif
   endfunction
   nmap <F9> mz:execute TabToggle()<CR>
endif


"Disable the annoying tab colors
let g:indent_guides_enable_on_vim_startup = 0

"Enable vim-airline status line (without having to split)
set laststatus=2

"Disables auto completions ( you can still use TAB to autocomplete )
"let g:neocomplcache_disable_auto_complete = 1
":call NeoCompleteLock()
"NOTE: to select an item hit <TAB> again
let g:neocomplete#disable_auto_complete = 1

"Escape no longer undoes the selection we just made ...
inoremap <expr> <Esc>  pumvisible() ? "\<C-y>\<Esc>" : "\<Esc>"

"The Status bar is too busy, this fixes that
let g:airline_extensions = []
let g:airline_theme = "wombat"

syntax enable
let g:solarized_termcolors=256
colorscheme solarized

"Block comment code in C/C++
autocmd FileType cpp,c let b:surround_45 = "#if 0\n\r\n#endif\n"

"Code block for markdown
autocmd FileType markdown let b:surround_45 = "```\n\r\n```"

"Block comment for python
autocmd FileType python let b:surround_45 = "\"\"\"\n\r\n\"\"\""

let NERDSpaceDelims = 1

set diffopt=filler,vertical

"MUCH better mouse control of the terminal
"  from http://usevim.com/2012/05/16/mouse/
set ttyfast
set mouse=a                 " Automatically enable mouse usage

if !has('nvim')
"  http://stackoverflow.com/questions/7000960/in-vim-why-doesnt-my-mouse-work-past-the-220th-column
   set ttymouse=sgr
endif

"Use Space/Shift-Tab key to switch between tabs
nmap <space> gt
nmap <S-Tab> gT

"Shortcut to split windows and move around between them
nmap ,v <C-w>v<C-w>l
" nmap ,s <C-w>s<C-w>j
nmap <C-h> <C-w>h
nmap <C-j> <C-w>j
nmap <C-l> <C-w>l
nmap <C-k> <C-w>k

"Shortcut to open NERDTree
nmap <Leader>ne :NERDTreeToggle<CR>

"from http://vimawesome.com/plugin/easymotion
" <Leader>f{char} to move to {char}
map  <Leader>f <Plug>(easymotion-bd-f)
nmap <Leader>f <Plug>(easymotion-overwin-f)

" Move to line
map <Leader>L <Plug>(easymotion-bd-jk)
nmap <Leader>L <Plug>(easymotion-overwin-line)

" Move to word
map  <Leader>w <Plug>(easymotion-bd-w)
nmap <Leader>w <Plug>(easymotion-overwin-w)
""""""""""""""""""""""""

"Stops vim from hiding quotes in json files
set conceallevel=0

let g:airline_powerline_fonts = 1
set history=1000                    " Store a ton of history (default is 20)
set spell                           " Spell checking on
set hidden                          " Allow buffer switching without saving

set backspace=indent,eol,start  " Backspace for dummies
set linespace=0                 " No extra spaces between rows
set number                      " Line numbers on
set showmatch                   " Show matching brackets/parenthesis
set incsearch                   " Find as you type search
set hlsearch                    " Highlight search terms
set winminheight=0              " Windows can be 0 line high
set ignorecase                  " Case insensitive search
set smartcase                   " Case sensitive when uc present
set wildmenu                    " Show list instead of just completing
set wildmode=list:longest,full  " Command <Tab> completion, list matches, then longest common part, then all.
set whichwrap=b,s,h,l,<,>,[,]   " Backspace and cursor keys wrap too
set scrolljump=5                " Lines to scroll when cursor leaves screen
set scrolloff=3                 " Minimum lines to keep above and below cursor
set foldenable                  " Auto fold code
set list
set listchars=tab:›\ ,trail:•,extends:#,nbsp:. " Highlight problematic whitespace

set nowrap                      " Do not wrap long lines
set autoindent                  " Indent at the same level of the previous line
set smartindent                 " Use code indenting style for auto indent
set tabstop=3                   " An indentation every three columns

function! StripTrailingWhitespace()
   " Preparation: save last search, and cursor position.
   let _s=@/
   let l = line(".")
   let c = col(".")
   " do the business:
   %s/\s\+$//e
   " clean up: restore previous search history, and cursor position
   let @/=_s
   call cursor(l, c)
endfunction

autocmd FileType * autocmd BufWritePre <buffer> call StripTrailingWhitespace()
set background=dark

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Cycle thru relativenumber + number, number (only) and no numbering
function! Cycle_numbering() abort
  if exists('+relativenumber')
    execute {
          \ '00': 'set relativenumber   | set number',
          \ '01': 'set norelativenumber | set number',
          \ '10': 'set norelativenumber | set nonumber',
          \ '11': 'set norelativenumber | set number' }[&number . &relativenumber]
  else
    " No relative numbering, just toggle numbers on and off.
    set number!<CR>
  endif
endfunction

nnoremap <silent> <Leader>r :call Cycle_numbering()<CR>
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""


nmap <F11> :!find $(pwd) -iname '*.c' -o -iname '*.cpp' -o -iname '*.cc' -o -iname '*.h' -o -iname '*.hpp' -o -iname '*.inl' > cscope.files<CR>
  \:!cscope -b -i cscope.files -f cscope.out<CR>
  \:cs reset<CR>

"Source the vimrc file after saving it
if has("autocmd")
   autocmd bufwritepost .vimrc source $MYVIMRC
   autocmd bufwritepost .myvimrc source $MYVIMRC
   autocmd bufwritepost .myvimrc_vundle_plugins source $MYVIMRC
endif

" Run any custom user vimrc setup last
try
   source ~/.myvimrc
catch
   " No such file? No problem; just ignore it
endtry
